function pos_feedback(cyto, EC50)
	(cyto^3/(cyto^3+EC50^3))+0.0001
end


function neg_feedback(cyto, EC50)
	(EC50^3/(cyto^3+EC50^3))+0.0001
end


function prob_feedback(conc, fbstren, EC50)
	((fbstren*conc+EC50)/(conc+EC50))+0.0001
end


# convert picomolar to molecules
function mol_to_molecules(mol)
	mol*6.02*1e23/(1e6*1e12)
end


model test
	########################## ODE cells ##############################################################
	###################################################################################################
	###################################################################################################
    # naive differentiation
    Naive' = -r_Naive*Naive
    Prec' = 2*r_Naive*Naive + (2*prolif_Prec*prob_Prec*r_Prec)*Prec1 - (r_Prec+death_Prec)*Prec

    # precursor differentiation
    Prec1' = r_Prec*Prec - (r_Prec+death_Prec)*Prec1

    # effector differentiation
    Th1' = n_div*prob_Th1*r_Prec*Prec1 + prolif_Th1*(2*Th1_2-Th1) - death_Th1*Th1
    Th1_2' = prolif_Th1*(Th1-Th1_2) - (death_Th1+r_Mem)*Th1_2 

    Tfh' = n_div*prob_Tfh*r_Prec*Prec1 + prolif_Tfh*(2*Tfh_2-Tfh) - death_Tfh*Tfh
    Tfh_2' = prolif_Tfh*(Tfh-Tfh_2) - (death_Tfh+r_Mem)*Tfh_2

    # chronic Tfh
    Tfhc' = n_div*prob_Tfhc*r_Prec*Prec1 + prolif_Tfhc*(2*Tfhc_2-Tfhc) - death_Tfhc*Tfhc
    Tfhc_2' = prolif_Tfhc*(Tfhc-Tfhc_2) - death_Tfhc*Tfhc_2
    
    # Tr1 cells
    Tr1' = n_div*prob_Tr1*r_Prec*Prec1 + prolif_Tr1*(2*Tr1_2-Tr1) - death_Tr1*Tr1
    Tr1_2' = prolif_Tr1*(Tr1-Tr1_2) - death_Tr1*Tr1_2


    # memory
    Th1_mem' = r_Mem*Th1_2
    Tfh_mem' = r_Mem*Tfh_2

	########################## ODE molecules ##########################################################
	###################################################################################################
	###################################################################################################
    IL2' = r_IL2*IL2_producers - deg_IL2*IL2
    IL10' = r_IL10*IL10_producers - deg_IL10*IL10    

    # molecules
    Myc' = -deg_Myc*Myc

    # antigen
    TCR' = -deg_TCR*TCR

    ########################## algebraic relation cytokines ###########################################
	###################################################################################################
	###################################################################################################
 	IL2_producers := Th1+Th1_2
 	IL10_producers := Tr1+Tr1_2

    ########################## algebraic relation Memory ##############################################
	###################################################################################################
	###################################################################################################
        
    r_Mem := r_Mem_base*neg_feedback(TCR, EC50_TCR)

	########################## algebraic relation proliferation #######################################
	###################################################################################################
	###################################################################################################
    prolif_TCR := prolif_TCR_base*pos_feedback(TCR, EC50_TCR)  
    prolif_Prec := prolif_Prec_base*pos_feedback(Myc, EC50_Myc) + prolif_TCR
    prolif_Th1 := prolif_Th1_base*pos_feedback(Myc, EC50_Myc)*prob_feedback(IL10, fb_IL10_Th1, EC50_IL10)
    prolif_Tfh := prolif_Tfh_base*pos_feedback(Myc, EC50_Myc)*prob_feedback(IL2, fb_IL2_Tfh, EC50_IL2)
    prolif_Tr1 := prolif_Tr1_base*pos_feedback(Myc, EC50_Myc)
    prolif_Tfhc := prolif_Tfhc_base*pos_feedback(Myc, EC50_Myc)*prob_feedback(IL2, fb_IL2_Tfh, EC50_IL2)
    
	########################## algebraic relation probability #########################################
	###################################################################################################
	###################################################################################################
    pTh1_fb := pTh1_base*neg_feedback(TCR, EC50_TCR) 
    pTfh_fb := pTfh_base*neg_feedback(TCR, EC50_TCR) 
    pTr1_fb := pTr1_base*pos_feedback(TCR, EC50_TCR) 
    pTfhc_fb := pTfhc_base*pos_feedback(TCR, EC50_TCR) 

    # normalize probabilities
    prob_Th1 := pTh1_fb*0.75 / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb)
    prob_Tfh := pTfh_fb*0.75 / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb)
    prob_Tr1 := pTr1_fb*0.75 / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb)
    prob_Prec = 0.25
    prob_Tfhc := pTfhc_fb*0.75 / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb)

	########################## parameters proliferation ###############################################
	###################################################################################################
	###################################################################################################
    prolif_TCR_base = 3.0
 	prolif_Th1_base = 4.5 # 4.5/alpha=2 = 2.25 division per day, 10.5 hours per division
 	prolif_Tfh_base = 4.5  # 4.5/alpha=2 = 2.25 division per day, 10.5 hours per division
 	prolif_Prec_base = 4.4  # 4.5/alpha=2 = 2.25 division per day, 10.5 hours per division
 	prolif_Tr1_base = 2.5
 	prolif_Tfhc_base = 1.0

	########################## parameters differentiation #############################################
	###################################################################################################
	###################################################################################################
    # previous params: 0.5 and 0.25
    r_Naive = 1.0
    r_Prec = 1.0
    r_Mem_base = 0.01
    n_div = 2
	########################## parameters probabilities ###############################################
	###################################################################################################
	###################################################################################################
    pTh1_base = 0.25
    pTfh_base = 0.15
    pTr1_base = 0.2
    pTfhc_base = 0.3
    # pPrec_base = 1-pTh1_base-pTfh_base-pTr1_base-pTfhc_base

	########################## parameters death #######################################################
	###################################################################################################
	###################################################################################################
    death_Prec = 0.24
    death_Th1 = 0.24
    death_Tfh = 0.24
    death_Tr1 = 0.035
    death_Tfhc = 0.02

	########################## parameters molecules ###################################################
	###################################################################################################
	###################################################################################################
    deg_Myc = 0.32
    EC50_Myc = 0.1 
    # TCR signaling
    deg_TCR = 10.0
    EC50_TCR = 0.5
 
 	########################## parameters cytokines ###################################################
	###################################################################################################
	###################################################################################################
    r_IL2 = 10.0*3600*24 # molecules/s * 60s*60s*24h to get molecules/day taken from literature
    r_IL10 = 10.0*3600*24 # # molecules/s * 60s*60s*24h to get molecules/day taken from literature
    deg_IL2 = 50000 # arbitrary value assuming global consumer pool of il2
    deg_IL10 = 10000 # arbitrary value assuming global consumer pool of il10

    # feedback effects (converted to molecules, assuming given lymph node volumne 1microlitre)
    EC50_IL2 = mol_to_molecules(6.5) # 6.5 estimated EC50 value in picomolar measured through stat phosphorylation
    EC50_IL10 = mol_to_molecules(50.0) # 50 estimated EC50 value in picomolar measured through stat phosphorylation

    fb_IL10_Th1 = 1
    fb_IL10_Tr1 = 1
    fb_IL2_Tfh = 1

	########################## initial conditions #####################################################
	###################################################################################################
	###################################################################################################
    # initial conditions
    Naive = 400
    Prec = 0
    Prec1 = 0
    Th1 = 0
    Th1_2 = 0
    Tfh = 0
    Tfh_2 = 0
    Tfhc = 0
    Tfhc_2 = 0
    Tr1 = 0
    Tr1_2 = 0
    Th1_mem = 0
    Tfh_mem = 0

    # initial conditions molecules
    Myc = 1
    TCR = 1

    # initial conditions cytokines
    IL10 = 0
    IL2 = 0

end