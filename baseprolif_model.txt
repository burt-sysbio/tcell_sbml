function cyto_conv(rate)
	rate*1e6*1e12/(1e23*6.02)
end

# convert picomolar to molecules
function mol_to_molecules(mol)
	mol*6.02*1e23/(1e6*1e12)
end

function pos_feedback(cyto, EC50)
	(cyto/(cyto+EC50))+0.0001
end


function prob_feedback(conc, fbstren, EC50)
	((fbstren*conc+EC50)/(conc+EC50))+0.0001
end


function neg_feedback(cyto, EC50)
	(EC50/(cyto+EC50))+0.0001
end

model test
	########################## ODE cells ##############################################################
	###################################################################################################
	###################################################################################################
    # naive differentiation
    Naive' = -r_Naive*Naive
    Precursor' = 2*r_Naive*Naive + (2*prolif_Prec*prob_Prec*r_Prec)*Precursor1 - (r_Prec+death_Prec)*Precursor

    # precursor differentiation
    Precursor1' = r_Prec*Precursor - (r_Prec+death_Prec)*Precursor1

    # effector differentiation
    Th1' = 4*prob_Th1*r_Prec*Precursor1 + prolif_Th1*(2*Th1_2-Th1) - (death_Th1+r_Th1_noIL2+r_Th1_Tfh+r_Th1_Tr1)*Th1
    Th1_2' = prolif_Th1*(Th1-Th1_2) - (death_Th1+r_Th1_noIL2+r_Th1_Tfh+r_Th1_Tr1)*Th1_2 

    # final effector
    Th1_noIL2' = r_Th1_noIL2*Th1 + prolif_Th1*(2*Th1_noIL2_2-Th1_noIL2) - (death_Th1+r_Mem+r_Th1_Tfh+r_Th1_Tr1)*Th1_noIL2
    Th1_noIL2_2' = prolif_Th1*(Th1_noIL2-Th1_noIL2_2) - (death_Th1+r_Mem+r_Th1_Tfh+r_Th1_Tr1)*Th1_noIL2_2
 
    Tfh' = 4*prob_Tfh*r_Prec*Precursor1 + prolif_Tfh*(2*Tfh_2-Tfh) + r_Th1_Tfh*(Th1+Th1_noIL2) - (death_Tfh+r_gcTfh+r_Tfh_chronic)*Tfh

    Tfh_2' = prolif_Tfh*(Tfh-Tfh_2) - (death_Tfh+r_gcTfh+r_Tfh_chronic)*Tfh_2

    # chronic Tfh
    r_Tfh_chronic := r_Tfh_chronic0*pos_feedback(TCR, EC50_TCR)

    gcTfh' = r_gcTfh*Tfh + prolif_Tfh*(2*gcTfh_2-gcTfh) - (death_Tfh+r_Mem+r_Tfh_chronic)*gcTfh
    gcTfh_2' =  prolif_Tfh*(gcTfh-gcTfh_2) - (death_Tfh+r_Mem+r_Tfh_chronic)*gcTfh_2

    Tfh_chronic' = prob_Tfh_chronic*r_Prec*Precursor1 + r_Tfh_chronic*Tfh - (death_Tfh_chronic+r_gcTfh)*Tfh_chronic
    gcTfh_chronic' = r_Tfh_chronic*gcTfh + r_gcTfh*Tfh_chronic - death_Tfh_chronic*gcTfh_chronic

    # Tr1 cells
    Tr1' = 4*prob_Tr1*r_Prec*Precursor1 + prolif_Tr1*(2*Tr1_2-Tr1) + r_Th1_Tr1*(Th1+Th1_noIL2) - death_Tr1*Tr1
    Tr1_2' = prolif_Tr1*(Tr1-Tr1_2) - death_Tr1*Tr1_2


    # memory
    Th1_mem' = r_Mem*Th1_noIL2
    Tfh_mem' = r_Mem*gcTfh

	########################## ODE molecules ##########################################################
	###################################################################################################
	###################################################################################################
 
    # molecules
    Myc' = -deg_Myc*Myc

    # cytokines
    IL2_consumers := Naive+Precursor+Precursor1+Th1+Th1_2+Th1_noIL2+Th1_noIL2_2
    IL2_producers := Naive+Precursor+Precursor1+Th1+Th1_2+Tfh+Tfh_2
    IL10_consumers := Precursor+Precursor1+Th1+Th1_2+Th1_noIL2+Th1_noIL2_2+Tr1+Tr1_2

    IL2' = r_IL2*IL2_producers - up_IL2*IL2_consumers*(IL2/(IL2+KC_IL2)) - deg_IL2*IL2
    IL10' = r_IL10*Tr1 - up_IL10*IL10_consumers*(IL10/(IL10+KC_IL10)) - deg_IL10*IL10
    IL10_ex' = r_IL10_ex*pos_feedback(TCR, EC50_TCR)*pos_feedback(IFNI, EC50_IFNI) - up_IL10*IL10_consumers*(IL10_ex/(IL10_ex+KC_IL10)) - deg_IL10*IL10_ex
    
    IFNI' = r_IFNI - deg_IFNI*IFNI*time

    # antigen
    TCR' = -deg_TCR*TCR


	########################## parameters differentiation #############################################
	###################################################################################################
	###################################################################################################
 
    # rate constants
    r_Naive = 0.5 
    r_Prec = 0.25
    r_Th1_noIL2 = 1.0
    r_Th1_Tr1_base = 0.25
    r_Th1_Tfh_base = 0.25
    r_Tfh_chronic0 = 15.0
    r_gcTfh = 0.25
    r_Mem = 0.004

    # differentiation rates
    r_Th1_Tr1 := r_Th1_Tr1_base*pos_feedback(TCR, EC50_TCR)*pos_feedback(IL10+IL10_ex, EC50_IL10)
    r_Th1_Tfh := r_Th1_Tfh_base*pos_feedback(TCR, EC50_TCR)

	########################## parameters proliferation ###############################################
	###################################################################################################
	###################################################################################################
    prolif_cyto0 = 1.0
    prolif_TCR0 = 12.5
 	prolif_Th10 = 4.4
 	prolif_Tfh0 = 5.4
 	prolif_Prec0 = 4.4
 	prolif_Tr10 = 4.0

    prolif_cyto := prolif_cyto0*pos_feedback(IL2, EC50_IL2)*neg_feedback(IL10+IL10_ex, EC50_IL10)
    prolif_TCR := prolif_TCR0*pos_feedback(TCR, EC50_TCR)
    
    prolif_Prec := prolif_Prec0*pos_feedback(Myc, EC50_Myc) + prolif_TCR
    prolif_Th1 := prolif_Th10*pos_feedback(Myc, EC50_Myc) + prolif_cyto 
    prolif_Tfh := prolif_Tfh0*pos_feedback(Myc, EC50_Myc)
    prolif_Tr1 := prolif_Tr10*pos_feedback(Myc, EC50_Myc)
 
	########################## parameters probabilities ###############################################
	###################################################################################################
	###################################################################################################
    pTh1_0 = 0.4
    pTfh_0 = 0.2
    pTr1_0 = 0.1
    pTfhc_0 = 0.1
    pPrec_0 = 1-pTh1_0-pTfh_0-pTr1_0-pTfhc_0

    # calculate feedback on probability
    fb_stren_pos = 2.0
    fb_stren_neg = 1/fb_stren_pos
    pTh1_fb := pTh1_0*prob_feedback(IL10+IL10_ex, fb_stren_neg, EC50_IL10)
    pTfh_fb := pTfh_0*prob_feedback(IL2, fb_stren_neg, EC50_IL2)
    pTr1_fb := pTr1_0*prob_feedback(IL10+IL10_ex, fb_stren_pos, EC50_IL10)
    pTfhc_fb := pTfhc_0*pos_feedback(TCR, EC50_TCR)

    # normalize probabilities
    prob_Th1 := pTh1_fb / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb+pPrec_0)
    prob_Tfh := pTfh_fb / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb+pPrec_0)
    prob_Tr1 := pTr1_fb / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb+pPrec_0)
    prob_Prec := pPrec_0 / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb+pPrec_0)
    prob_Tfh_chronic := pTfhc_fb / (pTfhc_fb+pTh1_fb+pTfh_fb+pTr1_fb+pPrec_0)

	########################## parameters death #######################################################
	###################################################################################################
	###################################################################################################
 
    death_Prec = 0.24
    death_Th1 = 0.24
    death_Tfh = 0.24
    death_chronic = 0.01
    death_Tr1 = 0.07
    death_Tfh_chronic = death_chronic

	########################## parameters cytokines ###################################################
	###################################################################################################
	###################################################################################################

    r_IFNI = 0.1
    EC50_IFNI = 1.0
    deg_IFNI = 1.0

    deg_Myc = 0.32
    EC50_Myc = 0.1 

    # TCR signaling
    deg_TCR = 10.0

    # cytokine production uptake degradation
    r_IL2 = 10.0*3600*24
    r_IL10 = 10.0*3600*24
    r_IL10_ex = 10000.0

    up_IL2 = 1.0*3600*24
    up_IL10 = 1.0*3600*24

 	KC_IL2 = mol_to_molecules(10.0)
    KC_IL10 = mol_to_molecules(10.0)

    deg_cyto_base = 0.1*24
    deg_IL2_consumers = 2000
    deg_IL10_consumers = 1000
    deg_IL2 = deg_cyto_base+deg_IL2_consumers
    deg_IL10 = deg_cyto_base+deg_IL10_consumers


    # feedback effects
    EC50_IL2 = mol_to_molecules(6.5)
    EC50_IL10 = mol_to_molecules(50.0)

    EC50_TCR = 0.5
	########################## initial conditions #####################################################
	###################################################################################################
	###################################################################################################
    # initial conditions
    Naive = 1200
    Precursor = 0
    Precursor1 = 0
    Th1 = 0
    Th1_2 = 0
    Th1_noIL2 = 0
    Th1_noIL2_2 = 0
    Th1_chronic = 0
    Tfh = 0
    Tfh_2 = 0
    Tfh_chronic = 0
    gcTfh_chronic = 0
    Tr1 = 0
    Tr1_2 = 0
    Th1_mem = 0
    gcTfh = 0
    gcTfh_2 = 0
    Tfh_mem = 0

    IL2 = 0
    IL10 = 0
    IL10_ex = 0

    # initial conditions molecules
    Myc = 1
    TCR = 1
    IFNI = 0

end